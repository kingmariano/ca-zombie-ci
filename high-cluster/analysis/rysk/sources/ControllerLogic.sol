/**
 * SPDX-License-Identifier: UNLICENSED
 */
pragma solidity =0.6.10;

pragma experimental ABIEncoderV2;

import {OwnableUpgradeSafe} from "../packages/oz/upgradeability/OwnableUpgradeSafe.sol";
import {ReentrancyGuardUpgradeSafe} from "../packages/oz/upgradeability/ReentrancyGuardUpgradeSafe.sol";
import {Initializable} from "../packages/oz/upgradeability/Initializable.sol";
import {SafeMath} from "../packages/oz/SafeMath.sol";
import {MarginVault} from "../libs/MarginVault.sol";
import {Actions} from "../libs/Actions.sol";
import {ERC20Interface} from "../interfaces/ERC20Interface.sol";
import {AddressBookInterface} from "../interfaces/AddressBookInterface.sol";
import {OtokenInterface} from "../interfaces/OtokenInterface.sol";
import {MarginCalculatorInterface} from "../interfaces/MarginCalculatorInterface.sol";
import {OracleInterface} from "../interfaces/OracleInterface.sol";
import {WhitelistInterface} from "../interfaces/WhitelistInterface.sol";
import {MarginPoolInterface} from "../interfaces/MarginPoolInterface.sol";
import {ControllerInterface} from "../interfaces/ControllerInterface.sol";

/**
 * @title Settlement
 * @author Rysk Finance
 * @notice Contract that handles logic for the Controller on a modified version of Opyn's Gamma protocol
 *         An extension of the Controller to avoid contract size issues. Error list can be found in Controller.sol
 *         Main functions callable by Controller
 */
contract ControllerLogic is Initializable, OwnableUpgradeSafe, ReentrancyGuardUpgradeSafe {
    using MarginVault for MarginVault.Vault;
    using SafeMath for uint256;

    AddressBookInterface public addressbook;
    WhitelistInterface public whitelist;
    MarginCalculatorInterface public calculator;
    OracleInterface public oracle;
    ControllerInterface public controller;
    MarginPoolInterface public pool;

    ///@dev scale used in MarginCalculator
    uint256 internal constant BASE = 8;

    ///@dev the number of seconds an ITM option is redeemable for after its expiry before the vault is settleable and collateral claimable.
    uint256 public redeemTimePeriod;

    /// @notice emits an event when a long oToken is deposited into a vault
    event LongOtokenDeposited(
        address indexed otoken,
        address indexed accountOwner,
        address indexed from,
        uint256 vaultId,
        uint256 amount
    );
    /// @notice emits an event when a long oToken is withdrawn from a vault
    event LongOtokenWithdrawed(
        address indexed otoken,
        address indexed AccountOwner,
        address indexed to,
        uint256 vaultId,
        uint256 amount
    );
    /// @notice emits an event when a collateral asset is deposited into a vault
    event CollateralAssetDeposited(
        address indexed asset,
        address indexed accountOwner,
        address indexed from,
        uint256 vaultId,
        uint256 amount
    );
    /// @notice emits an event when a collateral asset is withdrawn from a vault
    event CollateralAssetWithdrawed(
        address indexed asset,
        address indexed AccountOwner,
        address indexed to,
        uint256 vaultId,
        uint256 amount
    );
    /// @notice emits an event when a short oToken is minted from a vault
    event ShortOtokenMinted(
        address indexed otoken,
        address indexed AccountOwner,
        address indexed to,
        uint256 vaultId,
        uint256 amount
    );
    /// @notice emits an event when a short oToken is burned
    event ShortOtokenBurned(
        address indexed otoken,
        address indexed AccountOwner,
        address indexed from,
        uint256 vaultId,
        uint256 amount
    );
    /// @notice emits an event when a vault is settled
    event VaultSettled(
        address indexed accountOwner,
        address indexed oTokenAddress,
        address to,
        uint256 collateralPayout,
        uint256 strikePayout,
        uint256 vaultId,
        uint256 indexed vaultType
    );
    /// @notice emits an event when an oToken is redeemed
    event Redeem(
        address indexed otoken,
        address indexed redeemer,
        address indexed receiver,
        address collateralAsset,
        uint256 otokenBurned,
        uint256 payout
    );

    /// @notice emits an event when the redeem time period is updated
    event RedeemTimePeriodUpdated(uint256 previousPeriod, uint256 newPeriod);

    struct SettleMem {
        address collateral;
        address underlying;
        address strike;
        uint256 expiry;
        uint256 collateralPayout;
        uint256 strikePayout;
        uint256 strikeCount;
        uint256 shortAmount;
        bool isValidVault;
        uint256 collateralRedemptionBalance;
        uint256 receivingAssetBalance;
        uint256 otokenQuantity;
        uint256 underlyingExpiryPrice;
        uint256 collateralExpiryPrice;
    }

    /**
     * @notice check if the sender is the Controller module
     */
    modifier onlyController() {
        require(
            msg.sender == AddressBookInterface(addressbook).getController(),
            "ControllerLogic: Sender is not Controller"
        );

        _;
    }

    /**
     * @notice initalize the deployed contract
     * @param _addressbook addressbook module
     * @param _owner account owner address
     */
    function initialize(address _addressbook, address _owner) external initializer {
        require(_addressbook != address(0), "C7");
        require(_owner != address(0), "C8");

        __Ownable_init(_owner);
        __ReentrancyGuard_init_unchained();
        addressbook = AddressBookInterface(_addressbook);
        redeemTimePeriod = 3600;
    }

    /**
     * @dev updates the configuration of the controller. can only be called by the owner
     */
    function refreshConfiguration() external onlyOwner {
        _refreshConfigInternal();
    }

    /**
     * @notice get the collateral amount a redeemer receives for a given quantity of expired oTokens
     * @dev for cash-settled options: this is the cash value of the options in collateral terms.
     * for physically settled options: this is the collateral delivery amount (what the holder receives
     * in exchange for their strike payment during handleRedeem).
     * @param _otoken oToken address
     * @param _amount amount of oTokens to calculate the payout for (1e8 scaled)
     * @return amount of collateral to transfer to the redeemer
     */
    function getPayout(address _otoken, uint256 _amount) public view returns (uint256) {
        return calculator.getExpiredPayoutRate(_otoken).mul(_amount).div(10**BASE);
    }

    /**
     * @notice set ITM option redemption time period
     * @dev can only be called by owner
     * @param _redeemTimePeriod number of seconds an ITM option is redeemable for after its expiry
     */
    function setRedeemTimePeriod(uint256 _redeemTimePeriod) external onlyOwner {
        require(_redeemTimePeriod > 0, "C38");

        emit RedeemTimePeriodUpdated(redeemTimePeriod, _redeemTimePeriod);
        redeemTimePeriod = _redeemTimePeriod;
    }

    function handleDepositLong(Actions.DepositArgs memory _args) external onlyController {
        require(whitelist.isWhitelistedOtoken(_args.asset), "C17");

        OtokenInterface otoken = OtokenInterface(_args.asset);

        require(now < otoken.expiryTimestamp(), "C18");
        // dont allow deposit longs for physically settled type 2 vaults and physically settled otokens
        require(!otoken.isPhysicallySettled(), "C43");
        (, uint256 typeVault, ) = controller.getVaultWithDetails(_args.owner, _args.vaultId);
        require(typeVault != 2, "C43");

        controller.updateVault(2, _args.owner, _args.vaultId, _args.asset, _args.amount);

        pool.transferToPool(_args.asset, _args.from, _args.amount);

        emit LongOtokenDeposited(_args.asset, _args.owner, _args.from, _args.vaultId, _args.amount);
    }

    function handleWithdrawLong(Actions.WithdrawArgs memory _args) external onlyController {
        OtokenInterface otoken = OtokenInterface(_args.asset);

        require(now < otoken.expiryTimestamp(), "C19");
        controller.updateVault(3, _args.owner, _args.vaultId, _args.asset, _args.amount);

        pool.transferToUser(_args.asset, _args.to, _args.amount);

        emit LongOtokenWithdrawed(_args.asset, _args.owner, _args.to, _args.vaultId, _args.amount);
    }

    function handleDepositCollateral(Actions.DepositArgs memory _args) external onlyController {
        require(whitelist.isWhitelistedCollateral(_args.asset), "C21");

        controller.updateVault(4, _args.owner, _args.vaultId, _args.asset, _args.amount);

        pool.transferToPool(_args.asset, _args.from, _args.amount);

        emit CollateralAssetDeposited(_args.asset, _args.owner, _args.from, _args.vaultId, _args.amount);
    }

    function handleWithdrawCollateral(Actions.WithdrawArgs memory _args) external onlyController {
        (MarginVault.Vault memory vault, , ) = controller.getVaultWithDetails(
            _args.owner,
            _args.vaultId
        );

        if (_isNotEmpty(vault.shortOtokens)) {
            OtokenInterface otoken = OtokenInterface(vault.shortOtokens[0]);

            require(now < otoken.expiryTimestamp(), "C22");
        }

        controller.updateVault(5, _args.owner, _args.vaultId, _args.asset, _args.amount);

        pool.transferToUser(_args.asset, _args.to, _args.amount);

        emit CollateralAssetWithdrawed(_args.asset, _args.owner, _args.to, _args.vaultId, _args.amount);
    }

    function handleMintOtoken(Actions.MintArgs memory _args, uint256 vaultType) external onlyController {
        require(whitelist.isWhitelistedOtoken(_args.otoken), "C23");

        OtokenInterface otoken = OtokenInterface(_args.otoken);

        require(now < otoken.expiryTimestamp(), "C24");

        if (vaultType == 2) {
            // this is a physically settled vault
            require(otoken.isPhysicallySettled(), "C40");

            uint256 collateralDecimals = uint256(ERC20Interface(otoken.collateralAsset()).decimals());

            // prevent truncation in collateral tracking for tokens with fewer decimals than oTokens (8)
            if (collateralDecimals < 8) {
                require(_args.amount % (10**(8 - collateralDecimals)) == 0, "C46");
            }

            // update otokenQuantity and collateralBalance using canonical-balance approach:
            // compute collateralBalance from total otokenQuantity before and after, so the result is
            // independent of how many mints occur. this prevents rounding discrepancies between
            // split mints and combined burns/redeems.
            (, , uint256 oldQuantity) = pool.getRedemptionBalance(_args.otoken);
            uint256 newQuantity = oldQuantity.add(_args.amount);

            if(otoken.isPut()) {
                uint256 oldCollateral = oldQuantity.mul(otoken.strikePrice()).mul(10**collateralDecimals).div(1e16);
                uint256 newCollateral = newQuantity.mul(otoken.strikePrice()).mul(10**collateralDecimals).div(1e16);
                pool.updateRedemptionBalance(
                    _args.otoken,
                    int256(_args.amount),
                    false,
                    int256(newCollateral.sub(oldCollateral))
                );
            } else {
                uint256 oldCollateral = oldQuantity.mul(10**collateralDecimals).div(1e8);
                uint256 newCollateral = newQuantity.mul(10**collateralDecimals).div(1e8);
                pool.updateRedemptionBalance(
                    _args.otoken,
                    int256(_args.amount),
                    false,
                    int256(newCollateral.sub(oldCollateral))
                );
            }
        } else {
            // this is not a physically settled vault
            require(!otoken.isPhysicallySettled(), "C40");
        }

        controller.updateVault(0, _args.owner, _args.vaultId, _args.otoken, _args.amount);

        otoken.mintOtoken(_args.to, _args.amount);

        emit ShortOtokenMinted(_args.otoken, _args.owner, _args.to, _args.vaultId, _args.amount);
    }

    function handleBurnOtoken(Actions.BurnArgs memory _args, uint256 vaultType) external onlyController {
        OtokenInterface otoken = OtokenInterface(_args.otoken);

        // do not allow burning expired otoken
        require(now < otoken.expiryTimestamp(), "C26");

        if (vaultType == 2) {
            // this is a physically settled vault
            require(otoken.isPhysicallySettled(), "C40");

            uint256 collateralDecimals = uint256(ERC20Interface(otoken.collateralAsset()).decimals());

            // prevent truncation in collateral tracking for tokens with fewer decimals than oTokens (8)
            if (collateralDecimals < 8) {
                require(_args.amount % (10**(8 - collateralDecimals)) == 0, "C46");
            }

            // canonical-balance approach for burns (mirrors mint logic above)
            (, , uint256 oldQuantity) = pool.getRedemptionBalance(_args.otoken);
            uint256 newQuantity = oldQuantity.sub(_args.amount);

            if(otoken.isPut()) {
                uint256 oldCollateral = oldQuantity.mul(otoken.strikePrice()).mul(10**collateralDecimals).div(1e16);
                uint256 newCollateral = newQuantity.mul(otoken.strikePrice()).mul(10**collateralDecimals).div(1e16);
                pool.updateRedemptionBalance(
                    _args.otoken,
                    -int256(_args.amount),
                    false,
                    -int256(oldCollateral.sub(newCollateral))
                );
            } else {
                uint256 oldCollateral = oldQuantity.mul(10**collateralDecimals).div(1e8);
                uint256 newCollateral = newQuantity.mul(10**collateralDecimals).div(1e8);
                pool.updateRedemptionBalance(
                    _args.otoken,
                    -int256(_args.amount),
                    false,
                    -int256(oldCollateral.sub(newCollateral))
                );
            }

        } else {
            // this is not a physically settled vault
            require(!otoken.isPhysicallySettled(), "C40");
        }

        // remove otoken from vault
        controller.updateVault(1, _args.owner, _args.vaultId, _args.otoken, _args.amount);
        // burn otoken
        otoken.burnOtoken(_args.from, _args.amount);

        emit ShortOtokenBurned(_args.otoken, _args.owner, _args.from, _args.vaultId, _args.amount);
    }

    function handleRedeem(Actions.RedeemArgs memory _args, address sender) external onlyController {
        OtokenInterface otoken = OtokenInterface(_args.otoken);

        // check that otoken to redeem is whitelisted
        require(whitelist.isWhitelistedOtoken(_args.otoken), "C27");

        (address collateral, address underlying, address strike, uint256 expiry) = _getOtokenDetails(address(otoken));

        // only allow redeeming expired otoken
        require(now >= expiry, "C28");

        require(controller.canSettleAssets(underlying, strike, collateral, expiry), "C29");

        // prevent truncation in payout/payment calculations for tokens with fewer decimals than oTokens (8)
        uint256 collateralDecimals = uint256(ERC20Interface(collateral).decimals());
        if (collateralDecimals < 8) {
            require(_args.amount % (10**(8 - collateralDecimals)) == 0, "C46");
        }

        if (otoken.isPhysicallySettled()) {
            // must be within the redeem time period
            require(now < expiry + redeemTimePeriod, "C39");
            
            uint256 strikePrice = otoken.strikePrice();
            (uint256 underlyingExpiryPrice,) = oracle.getExpiryPrice(underlying, expiry);
            (uint256 collateralExpiryPrice,) = oracle.getExpiryPrice(collateral, expiry);

            // get the amount of capital needed to exercise.
            // for calls this is strikeAsset, for puts it is underlyingAsset
            uint256 strikePayment = calculator.getStrikePaymentAmount(_args.otoken, _args.amount); 

            if(otoken.isPut()) {
                // check put is ITM
                require(underlyingExpiryPrice < strikePrice, "C45");
                // take the underlying asset payment
                _removeExcessCollateralFromRedemptionBalances(otoken, strikePrice, underlyingExpiryPrice, collateralExpiryPrice);
                pool.transferToPool(otoken.underlyingAsset(), _args.receiver, strikePayment);
            } else {
                // check call is ITM
                require(underlyingExpiryPrice > strikePrice, "C45");

                _removeExcessCollateralFromRedemptionBalances(otoken, strikePrice, underlyingExpiryPrice, collateralExpiryPrice);
                // take the strike asset payment (zero-strike calls have no payment so skip to avoid MarginPool revert)
                if (strikePayment > 0) {
                    pool.transferToPool(otoken.strikeAsset(), _args.receiver, strikePayment);
                }
            }
            
            pool.updateRedemptionBalance(_args.otoken, 0, true, int256(strikePayment));
        }
        uint256 payout = getPayout(_args.otoken, _args.amount);
        otoken.burnOtoken(sender, _args.amount);

        pool.transferToUser(collateral, _args.receiver, payout);
        
        if (otoken.isPhysicallySettled()) {
            pool.updateRedemptionBalance(_args.otoken, 0, false, -int256(payout));
        }

        emit Redeem(_args.otoken, sender, _args.receiver, collateral, _args.amount, payout);
    }

    function handleSettle(Actions.SettleVaultArgs memory _args) external onlyController {
        (MarginVault.Vault memory vault, uint256 typeVault, ) = controller.getVaultWithDetails(
            _args.owner,
            _args.vaultId
        );

        // check if there is short or long otoken in vault
        // do not allow settling vault that have no short or long otoken
        // if there is a long otoken, burn it
        // store otoken address outside of this scope
        
        bool hasShort = _isNotEmpty(vault.shortOtokens);
        bool hasLong = _isNotEmpty(vault.longOtokens);

        require(hasShort || hasLong, "C30");

        OtokenInterface otoken = hasShort ? OtokenInterface(vault.shortOtokens[0]) : OtokenInterface(vault.longOtokens[0]);

        if (hasLong) {
            OtokenInterface longOtoken = OtokenInterface(vault.longOtokens[0]);

            longOtoken.burnOtoken(address(pool), vault.longAmounts[0]);
            pool.decrementBalanceAfterBurn(vault.longOtokens[0], vault.longAmounts[0]);
        }

        SettleMem memory settleMem;
        ( settleMem.collateral, settleMem.underlying, settleMem.strike, settleMem.expiry) = _getOtokenDetails(address(otoken));

        // do not allow settling vault with un-expired otoken
        if (typeVault == 2) {
            require(now >= settleMem.expiry + redeemTimePeriod, "C41");
        } else {
            require(now >= settleMem.expiry, "C31");
        }
        require(controller.canSettleAssets(settleMem.underlying, settleMem.strike, settleMem.collateral, settleMem.expiry), "C29");

        // getExcessCollateral calculates collateral obligations differently post-expiry depending on settlement type (see _getCashValue):
        // - Cash-settled: the obligation is the cash value of the option (max(underlyingPrice - strike, 0) for calls). All collateral
        //   beyond this obligation is excess and can be returned to the writer.
        // - Physically-settled: the obligation is the amount of collateral that would be redeemed by the option buyer in exchange for
        //   the strike payment (underlyingPrice for ITM calls, strikePrice for ITM puts). This is larger than the cash value because
        //   it represents the full delivery amount, not just the profit.
        // For OTM options (both types), the obligation is zero — the full collateral amount is returned as excess.
        (settleMem.collateralPayout, settleMem.isValidVault) = calculator.getExcessCollateral(vault, typeVault);

        // require that vault is valid (has excess collateral) before settling.
        // the calculator's peg-equivalent depeg cap ensures that under LST depeg, the per-oToken obligation
        // is capped at the pegged rate, so properly collateralized vaults always report as valid.
        require(settleMem.isValidVault, "C32");

        // For physically settled vaults, the writer is also entitled to their pro-rata share of the
        // redemptionBalances pool for this oToken series. This pool tracks both unredeemed collateral
        // (from options that expired unexercised) and strike payments received from buyers who exercised.
        // The following block calculates the writer's share of these balances, added on top of any
        // excess collateral already computed above.
        if (typeVault == 2) {
            uint256 strikePrice = otoken.strikePrice();
            (settleMem.underlyingExpiryPrice,) = oracle.getExpiryPrice(settleMem.underlying, settleMem.expiry);
            (settleMem.collateralExpiryPrice,) = oracle.getExpiryPrice(settleMem.collateral, settleMem.expiry);

            _removeExcessCollateralFromRedemptionBalances(
                otoken, 
                strikePrice,
                settleMem.underlyingExpiryPrice,
                settleMem.collateralExpiryPrice
            );
            
            (settleMem.collateralRedemptionBalance, settleMem.receivingAssetBalance, settleMem.otokenQuantity) = pool.getRedemptionBalance(address(otoken));

            settleMem.shortAmount = vault.shortAmounts[0];

            // CALLS:
            // strike payment is number of options in vault * strike price, converted to strike decimals
            // strikePrice denominatd in e8, shortAmount denominated in e8
            // strikePrice * shortAmount * e2 = e18
            // divide by (18 - strike decimals) to convert to strike decimals
            // assumes strike asset decimals <= 18
            // PUTS:
            // strike payment is number of options in vault, converted to underying decimals
            // shortAmount * e10 = e18
            // divide by (18 - underlying decimals) to convert to underlying decimals
            settleMem.strikePayout = otoken.isPut() ? 
                settleMem.shortAmount.mul(1e10).div(10**(18 - uint256(ERC20Interface(settleMem.underlying).decimals()))) :
                (strikePrice.mul(settleMem.shortAmount).mul(1e2)).div(10**(18 - uint256(ERC20Interface(settleMem.strike).decimals()))
            );

            // From here there are three scenarios that can happen:
            // 0. (IF OTM) There is no strike asset in the pool. Meaning the user should receive their payment in collateral + any collateral they have from overcollateralisation
            // 1. (IF ITM and no strike asset in the pool) Meaning the user should receive their payment in collateral + any collateral they have from overcollateralisation 
            // 2. (IF ITM not enough strike asset in the pool to pay the strike payment) Meaning the user should receive any remaining strike asset, their remaining payment in collateral + any collateral they have from overcollateralisation
            // 3. (IF ITM and there is enough strike asset in the pool to pay the strike payment) Meaning the user should receive their payment in strike + any collateral they have from overcollateralization
            if (settleMem.receivingAssetBalance == 0) {
                // this is scenario 0 or 1
                settleMem.strikePayout = 0;
                // each remaining otoken to settle gets an equal share of collateralRedemptionBalance, (for OTM options this is zero)
                uint256 vaultShareOfCollateralRedemptionBalance = settleMem.collateralRedemptionBalance.mul(settleMem.shortAmount).div(settleMem.otokenQuantity);
                settleMem.collateralPayout += vaultShareOfCollateralRedemptionBalance;

                pool.updateRedemptionBalance(address(otoken), -int256(settleMem.shortAmount), false, -int256(vaultShareOfCollateralRedemptionBalance));
            } else if (settleMem.strikePayout > settleMem.receivingAssetBalance) {
                // this is scenario 2
                settleMem.strikePayout = settleMem.receivingAssetBalance;
                // find how many options worth of strike payment is in the pool
                // CALLS: get receivingAssetBalance and convert to e18 notation then divide by strikePrice * 1e2 (e10) to get e8 value.
                // PUTS: get receivingAssetBalance and convert to e8 notation
                // assumes strike asset decimals <= 18
                settleMem.strikeCount = otoken.isPut() ? 
                    settleMem.receivingAssetBalance.mul(10**(18 - uint256(ERC20Interface(settleMem.underlying).decimals()))).div(1e10) :
                    settleMem.receivingAssetBalance.mul(10**(18 - uint256(ERC20Interface(settleMem.strike).decimals()))).div(strikePrice.mul(1e2));
                // we can get the amount the user is owed in collateral asset by subtracting the strikeCount from the shortAmounts
                uint256 contractsLeft = settleMem.shortAmount.sub(settleMem.strikeCount);
                // divide collateralRedemptionBalance by total number of options to split between (otokenQuantity - strikeCount) and multiply by contractsLeft
                uint256 vaultShareOfCollateralRedemptionBalance = settleMem.collateralRedemptionBalance.mul(contractsLeft).div(settleMem.otokenQuantity.sub(settleMem.strikeCount));
                // the collateral payout is whatever it already was + the share of the collateralRedemptionBalance
                settleMem.collateralPayout += vaultShareOfCollateralRedemptionBalance;
                pool.updateRedemptionBalance(address(otoken), -int256(settleMem.strikeCount), true, -int256(settleMem.strikePayout));
                pool.updateRedemptionBalance(address(otoken), -int256(contractsLeft), false, -int256(vaultShareOfCollateralRedemptionBalance));
            } else {
                // this is scenario 3
                pool.updateRedemptionBalance(address(otoken), -int256(settleMem.shortAmount), true, -int256(settleMem.strikePayout));
            }
        }

        controller.updateVault(6, _args.owner, _args.vaultId, address(0), 0);

        if (settleMem.collateralPayout > 0) {
            pool.transferToUser(settleMem.collateral, _args.to, settleMem.collateralPayout);
        }
        if (settleMem.strikePayout > 0) {
            pool.transferToUser(otoken.isPut() ? otoken.underlyingAsset() : otoken.strikeAsset(), _args.to, settleMem.strikePayout);
        }
        uint256 vaultId = _args.vaultId;
        address payoutRecipient = _args.to;

        emit VaultSettled(_args.owner, address(otoken), payoutRecipient, settleMem.collateralPayout, settleMem.strikePayout, vaultId, typeVault);
    }

    function _removeExcessCollateralFromRedemptionBalances(
         OtokenInterface otoken,
         uint256 strikePrice,
         uint256 underlyingExpiryPrice,
         uint256 collateralExpiryPrice
    ) internal {
        // At mint time, collateralRedemptionBalance is set to 1 unit of collateral per oToken (for calls). But after expiry,
        // not all of that collateral is owed to option buyers — only the portion worth 1 unit of underlying (the delivery obligation).
        // If the collateral is yield-bearing (e.g. kHYPE > HYPE), there is excess collateral per option beyond the delivery amount.
        // This function removes that excess once, so the remaining collateralRedemptionBalance can be split pro-rata among
        // writers whose vaults were not fully paid out in strike asset (scenarios 0/1/2 in handleSettle).
        
        (uint256 collateralRedemptionBalance, , uint256 otokenQuantity) = pool.getRedemptionBalance(address(otoken));
        uint256 totalExcessInPool;

        if(
            otoken.isPut() && underlyingExpiryPrice >= strikePrice ||
            !otoken.isPut() && underlyingExpiryPrice <= strikePrice
        ){
            // options expired OTM
            // no receiving asset consideration, vaults receive all their collateral back
            // set collateralRedemptionBalance to 0 since calculator.getExcessCollateral handles this case (because short options are worthless)
            // return if already set to 0
            if (collateralRedemptionBalance == 0) return;

            totalExcessInPool = collateralRedemptionBalance;
        } else if(otoken.isPut()) {
            // dont need to handle ITM put cases because collateralAsset == strikeAsset, so fully collateralised with no excess.
            return;
        } else if(!otoken.isPut()) {
            // ITM calls
            // first, check to see if this has already been done. We only want to do it once, the first time a settle OR redeem is done.
            // if is has NOT been done, collateralRedemptionBalance == otokenQuantity (accounting for decimals).
            // so we can return if this is not the case because it has already been done

            uint256 otokenQuantityCollateralDecimals = otokenQuantity.mul(10**uint256(ERC20Interface(otoken.collateralAsset()).decimals())).div(1e8);

            if (collateralRedemptionBalance != otokenQuantityCollateralDecimals) return;
            
            // calls expired ITM
            // excess collateral for each option in pool is 1 - underlyingPrice/collateralPrice
            // if underlyingPrice >= collateralPrice, there is no excess: set to 0
            totalExcessInPool = underlyingExpiryPrice < collateralExpiryPrice ? 
                otokenQuantityCollateralDecimals.sub(otokenQuantityCollateralDecimals.mul(underlyingExpiryPrice).div(collateralExpiryPrice)) :
                0;    
        }

        // reduce value in redemptionBalancePool
        pool.updateRedemptionBalance(
            address(otoken), 
            0, 
            false, 
            -int(totalExcessInPool)
        );
    }

    /**
     * @dev get otoken details
     */
    function _getOtokenDetails(address _otoken)
        internal
        view
        returns (
            address,
            address,
            address,
            uint256
        )
    {
        OtokenInterface otoken = OtokenInterface(_otoken);
        (address collateral, address underlying, address strike, , uint256 expiry, , ) = otoken.getOtokenDetails();
        return (collateral, underlying, strike, expiry);
    }

    function _isNotEmpty(address[] memory _array) internal pure returns (bool) {
        return (_array.length > 0) && (_array[0] != address(0));
    }

    /**
     * @dev updates the internal configuration of the controller
     */
    function _refreshConfigInternal() internal {
        whitelist = WhitelistInterface(addressbook.getWhitelist());
        controller = ControllerInterface(addressbook.getController());
        oracle = OracleInterface(addressbook.getOracle());
        calculator = MarginCalculatorInterface(addressbook.getMarginCalculator());
        pool = MarginPoolInterface(addressbook.getMarginPool());
    }
}


